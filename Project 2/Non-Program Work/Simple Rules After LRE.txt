Pgm -> kwdprog Vargroup Fcndefs Main
Main -> kwdmain BBlock
BBlock -> brace1 Vargroup Stmts brace2
Vargroup -> kwdvars PPvarlist
Vargroup -> EPSILON
PPvarlist -> parens1 Varlist parens2
Varlist -> Varitem semi Varlist
Varlist -> EPSILON
Varitem -> Vardecl
Varitem -> Vardecl equal Varinit
Varitem -> Classdecl
Varitem -> Classdef
Vardecl -> Simplekind Varspec
Simplekind -> Basekind
Simplekind -> Classid
Basekind -> kint
Basekind -> kfloat
Basekind -> kstring
Classid -> id
Varspec -> Varid
Varspec -> Arrspec
Varspec -> Deref_id
Varid -> id
Arrspec -> Varid KKint
KKint -> bracket1 int bracket2
Deref_id -> Deref id
Deref -> aster
Varinit -> Expr
Varinit -> BBexprs
BBexprs -> brace1 U

Exprlist -> Expr Moreexprs
Moreexprs -> comma Exprlist
Moreexprs -> EPSILON
Classdecl -> kwdclass Classid
Classdef -> Classheader BBClassitems
Classdef -> Classheader kif BBClassitems
BBClassitems -> brace1 Classitems brace2
Classheader -> Classdecl Classmom Interfaces
Classmom -> colon Classid
Classmom -> EPSILON
Classitems -> Classgroup Classitems
Classitems -> EPSILON
Classgroup -> Class_ctrl
Classgroup -> Vargroup
Classgroup -> Mddecls
Class_ctrl -> colon id
Interfaces -> plus Classid Interfaces
Interfaces -> EPSILON
Mddecls -> Mdheader Mddecls
Mddecls -> EPSILON
Mdheader -> kwdfcn Md_id PParmlist Retkind
Md_id -> Classid colon Fcnid
Fcndefs -> Fcndef Fcndefs
Fcndefs -> EPSILON
Fcndef -> Fcnheader BBlock
Fcnheader -> kwdfcn Fcnid PParmlist Retkind
Fcnid -> id
Retkind -> Basekind
PParmlist -> parens1 Varspecs parens2
PParmlist -> PPonly
Varspecs -> Varspec More_varspecs
More_varspecs -> comma Varspecs
More_varspecs -> EPSILON
PPonly -> parens1 parens2
Stmts -> Stmt semi Stmts
Stmts -> EPSILON
Stmt -> Stasgn
Stmt -> Fcall
Stmt -> Stif
Stmt -> Stwhile
Stmt -> Stprint
Stmt -> Strtn
Stasgn -> Lval equal Expr
Lval -> Varid
Lval -> Aref
Lval -> Deref_id
Aref -> Varid KKexpr
KKexpr -> bracket1 Expr bracket2
Fcall -> Fcnid PPexprs
PPexprs -> parens1 Exprlist parens2
PPexprs -> PPonly
Stif -> kwdif PPexpr BBlock Elsepart
Elsepart -> kwdelseif PPexpr BBlock Elsepart
Elsepart -> kwdelse BBlock
Elsepart -> EPSILON
Stwhile -> kwdwhile PPexpr BBlock
Stprint -> kprint PPexprs
Strtn -> kwdreturn T

PPexpr -> parens1 Expr parens2
Q -> EPSILON
Q -> Oprel Rterm Q
Expr -> Rterm Q
R -> EPSILON
R -> Opadd Term R
Rterm -> Term R
S -> EPSILON
S -> Opmul Fact S
Term -> Fact S
Fact -> BaseLiteral
Fact -> Lval
Fact -> Addrof_id
Fact -> Fcall
Fact-> PPexpr
BaseLiteral -> int
BaseLiteral -> float
BaseLiteral -> string
Addrof_id -> ampersand id
Oprel -> opeq
Oprel -> opne
Oprel -> Lthan
Oprel -> ople
Oprel -> opge
Oprel -> Gthan
Lthan -> angle1
Gthan -> angle2
Opadd -> plus
Opadd -> minus
Opmul -> aster
Opmul -> slash
Opmul -> caret
T -> EPSILON
T -> Expr
U -> Exprlist brace2
U -> brace2 



More epsilon rules here